dnl Process this with autoconf to create configure
AC_INIT(doc/kawa.texi)

AM_INIT_AUTOMAKE(kawa, 1.6.71)

AM_MAINTAINER_MODE
AC_PROG_CC
AC_EXEEXT

AC_ARG_PROGRAM
dnl AC_ARG_WITH(classpath,[  --with-classpath          Path to Java classes], classpath=$withval, classpath=$$CLASSPATH)
AC_ARG_ENABLE(kawa-frontend,
  [  --enable-kawa-frontend   Build "kawa" front-end program using readline],
  enable_kawa_frontend=$enableval, enable_kawa_frontend=no)
if test $enable_kawa_frontend = yes ; then
  kawa_bin=kawa
fi

AC_ARG_ENABLE(xml,
  [  --enable-xml             Build support for XML processing])

AC_ARG_WITH(swing,
  [  --with-swing  Assume Swing (Java Foundation Classes) is available])

AC_ARG_WITH(awt,
  [  --with-awt  Assume AWT is available (default on)])

#AM_PROG_INSTALL
#AC_PROG_MAKE_SET

JAVA=${JAVA-java}
JAVAC=${JAVAC-javac}
AC_SUBST(JAVA)
AC_SUBST(JAVAC)
AC_SUBST(kawa_bin)
dnl We must have the absolute path to the build directory.
TOP_BUILDDIR=`pwd`

case ${with_swing} in
  yes)
    extra_java='$(java_SWING)'
    SWING_SELECTED=WITH_SWING
    ;;
  *)
    extra_java=''
    SWING_SELECTED=WITHOUT_SWING
    ;;
esac
  
case ${with_awt} in
  no) AWT_SELECTED=WITHOUT_AWT ;;
  *)  AWT_SELECTED=WITH_AWT ;;
esac

case ${enable_xml} in
  yes)  XML_SELECTED=XML_ENABLED ;;
  *)  XML_SELECTED=XML_DISABLED ;;
esac
  
JEMACS_VERSION=`echo $VERSION | sed  -e 's/^1/0/'`

AC_SUBST(XML_SELECTED)
AC_SUBST(AWT_SELECTED)
AC_SUBST(SWING_SELECTED)
AC_SUBST(extra_java)
AC_SUBST(TOP_BUILDDIR)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(JEMACS_VERSION)
AC_SUBST(SHELL)

AC_OUTPUT(Makefile gnu/Makefile gnu/bytecode/Makefile gnu/math/Makefile
  gnu/text/Makefile gnu/mapping/Makefile gnu/expr/Makefile
  gnu/kawa/Makefile gnu/kawa/util/Makefile gnu/kawa/reflect/Makefile 
  gnu/kawa/functions/Makefile gnu/kawa/xml/Makefile
  gnu/kawa/lispexpr/Makefile gnu/kawa/slib/Makefile gnu/ecmascript/Makefile
  gnu/commonlisp/Makefile gnu/commonlisp/lang/Makefile
  gnu/commonlisp/testsuite/Makefile
  gnu/jemacs/Makefile gnu/jemacs/lang/Makefile gnu/jemacs/buffer/Makefile
  gnu/jemacs/lisp/Makefile gnu/jemacs/testsuite/Makefile
  kawa/Makefile kawa/lang/Makefile kawa/standard/Makefile kawa/lib/Makefile
  doc/Makefile testsuite/Makefile)
